pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            name = 'MinecraftForge'
            url = 'https://maven.minecraftforge.net/'
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            library('jopt-simple', 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3')
            library('srgutils', 'net.minecraftforge:srgutils:0.5.10')
            library('nulls', 'org.jetbrains:annotations:24.0.1')
            library('powermock', 'org.powermock:powermock-core:2.0.9')

            version('asm', '9.7.1')
            library('asm',         'org.ow2.asm', 'asm'        ).versionRef('asm')
            library('asm-commons', 'org.ow2.asm', 'asm-commons').versionRef('asm')
            library('asm-tree',    'org.ow2.asm', 'asm-tree'   ).versionRef('asm')
            bundle('asm', ['asm', 'asm-commons', 'asm-tree'])

            version('junit', '5.10.1')
            library('junit-api', 'org.junit.jupiter', 'junit-jupiter-api').versionRef('junit')
            library('junit-engine', 'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit')
            library('junit-platform-launcher', 'org.junit.platform:junit-platform-launcher:1.10.1')
            bundle('junit-runtime', ['junit-engine', 'junit-platform-launcher'])
        }
    }
}

rootProject.name = 'ForgeAutoRenamingTool'
