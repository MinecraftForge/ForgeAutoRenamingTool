import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group 'net.minecraftforge'
version = gradleutils.getTagOffsetBranchVersion(null, 'master', 'HEAD')
println "Version: $version"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'net.minecraftforge:srgutils:0.5.1'
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-commons:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'

    testImplementation platform('org.junit:junit-bom:5.8.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.powermock:powermock-core:2.0.9'

    compileOnly 'org.jetbrains:annotations:24.0.1'
}

tasks.named('test', Test).configure {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

tasks.named('shadowJar', ShadowJar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
                'Specification-Title':      'ForgeAutoRenamingTool',
                'Specification-Vendor':     'Forge Development LLC',
                'Specification-Version':    gradleutils.gitInfo.tag,
                'Implementation-Title':     'ForgeAutoRenamingTool',
                'Implementation-Version':   project.version,
                'Implementation-Vendor':    'Forge Development LLC'
        ], 'net/minecraftforge/fart/')
    }

    minimize()

    final List<String> relocations = [
            'org.objectweb.asm',
            'net.minecraftforge.srgutils',
            'joptsimple'
    ]

    relocations.each {
        relocate it, "net.minecraftforge.fart.relocated.$it"
    }
}

tasks.named('assemble').configure {
    dependsOn 'shadowJar'
}

tasks.named('compileJava', JavaCompile).configure {
    options.encoding = 'UTF-8'
}

changelog {
    fromTag '0.1'
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        pom {
            name = 'Forge Auto Renaming Tool'
            description = 'A tool that renames java bytecode elements.'
            url = 'https://github.com/MinecraftForge/ForgeAutoRenamingTool'

            PomUtils.setGitHubDetails(pom, 'ForgeAutoRenamingTool')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}
