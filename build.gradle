import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
}

group = 'net.minecraftforge'
version = gradleutils.getTagOffsetBranchVersion(null, 'master', 'HEAD')
println "Version: $version"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    implementation(libs.jopt.simple)
    api(libs.srgutils)
    implementation(libs.bundles.asm)

    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.bundles.junit.runtime)
    testImplementation(libs.powermock)

    compileOnly(libs.nulls)
}

tasks.named('test', Test).configure {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
                'Specification-Title':      'ForgeAutoRenamingTool',
                'Specification-Vendor':     'Forge Development LLC',
                'Specification-Version':    gradleutils.gitInfo.tag,
                'Implementation-Title':     'ForgeAutoRenamingTool',
                'Implementation-Version':   project.version,
                'Implementation-Vendor':    'Forge Development LLC'
        ], 'net/minecraftforge/fart/')
    }
}

tasks.named('shadowJar', ShadowJar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
                'Specification-Title':      'ForgeAutoRenamingTool',
                'Specification-Vendor':     'Forge Development LLC',
                'Specification-Version':    gradleutils.gitInfo.tag,
                'Implementation-Title':     'ForgeAutoRenamingTool',
                'Implementation-Version':   project.version,
                'Implementation-Vendor':    'Forge Development LLC'
        ], 'net/minecraftforge/fart/')
    }

    minimize()

    final List<String> relocations = [
            'org.objectweb.asm',
            'net.minecraftforge.srgutils',
            'joptsimple'
    ]

    relocations.each {
        relocate it, "net.minecraftforge.fart.relocated.$it"
    }
}

tasks.named('assemble').configure {
    dependsOn 'shadowJar'
}

tasks.named('compileJava', JavaCompile).configure {
    options.encoding = 'UTF-8'
}

changelog {
    from '0.1'
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        pom {
            name = 'Forge Auto Renaming Tool'
            description = 'A tool that renames java bytecode elements.'
            url = 'https://github.com/MinecraftForge/ForgeAutoRenamingTool'

            gradleutils.pom.setGitHubDetails(pom, 'ForgeAutoRenamingTool')

            license gradleutils.pom.Licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
