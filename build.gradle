plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group 'net.minecraftforge'
version = gradleutils.getTagOffsetBranchVersion(null, 'master', 'HEAD')
println('Version: ' + version)

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

tasks.named('compileJava', JavaCompile).configure {
    options.encoding = 'UTF-8'
}

final String REPO = 'ForgeAutoRenamingTool'
final String TITLE = 'Forge Auto Renaming Tool'
final MANIFEST = manifest {
    attributes('Main-Class': 'net.minecraftforge.fart.Main')
    attributes([
            'Specification-Title':      'ForgeAutoRenamingTool',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    gradleutils.gitInfo.tag,
            'Implementation-Title':     'SimpleInstaller',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
    ], 'net/minecraftforge/fart/')
}
final String INITIAL_CHANGELOG_TAG = '0.1'

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
}

license {
    header = file('LICENSE-header.txt')
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'net.minecraftforge:srgutils:0.5.1'
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-commons:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'

    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.powermock:powermock-core:2.0.9')

    compileOnly 'org.jetbrains:annotations:24.0.1'
}


tasks.named('test', Test).configure {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    manifest.from(MANIFEST)
    minimize()

    final List<String> relocations = [
            'org.objectweb.asm',
            'net.minecraftforge.srgutils',
            'joptsimple'
    ]

    relocations.each {
        relocate it, "net.minecraftforge.fart.relocated.$it"
    }
}

tasks.named('assemble').configure {
    dependsOn shadowJar
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        pom {
            name = TITLE
            description = 'A tool that renames java bytecode elements.'
            url = "https://github.com/MinecraftForge/${REPO}"
            scm {
                url = "https://github.com/MinecraftForge/${REPO}"
                connection = "scm:git:git://github.com/MinecraftForge/${REPO}.git"
                developerConnection = "scm:git:git@github.com:MinecraftForge/${REPO}.git"
            }
            issueManagement {
                system = 'github'
                url = "https://github.com/MinecraftForge/${REPO}/issues"
            }

            licenses {
                license {
                    name = 'LGPLv2.1'
                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                }
            }

            developers {
                developer {
                    id = 'LexManos'
                    name = 'LexManos'
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

changelog {
    fromTag INITIAL_CHANGELOG_TAG
}
