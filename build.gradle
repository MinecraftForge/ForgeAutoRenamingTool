import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradleutils'
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.licenser
    alias libs.plugins.shadow
}

gradleutils.displayName = 'Forge Auto Renaming Tool'
description = 'A command line tool to rename java class files'
group = 'net.minecraftforge'
version = gitversion.tagOffset
println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    implementation(libs.jopt.simple)
    api(libs.srgutils)
    implementation(libs.bundles.asm)

    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.bundles.junit.runtime)
    testImplementation(libs.powermock)

    compileOnly(libs.nulls)
}

tasks.named('test', Test) {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

tasks.named('jar') {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
            'Specification-Title':      'ForgeAutoRenamingTool',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    gitversion.info.tag,
            'Implementation-Title':     'ForgeAutoRenamingTool',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ], 'net/minecraftforge/fart/')
    }
}

tasks.named('shadowJar') {
    minimize()
    enableAutoRelocation = true
    relocationPrefix = 'net.minecraftforge.fart.relocated'
    // Rewrite JOpt's message files, so that help text is displayed nicely.
    transform(PropertiesFileTransformer) {
        paths = [ 'Messages.properties$' ]
        keyTransformer = { key -> "net.minecraftforge.fart.relocated.$key".toString() }
    }
}

tasks.named('assemble') {
    dependsOn 'shadowJar'
}

tasks.named('compileJava', JavaCompile) {
    options.encoding = 'UTF-8'
}

changelog {
    from '0.1'
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        pom {
            name = 'Forge Auto Renaming Tool'
            description = 'A tool that renames java bytecode elements.'
            url = 'https://github.com/MinecraftForge/ForgeAutoRenamingTool'

            gradleutils.pom.addRemoteDetails(pom)

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
